# Maintainer: Mark Givens <marcxjo@cerebralbleach.com>
# Contributor: Simon Thorpe <simon@hivetechnology.com.au>

pkgname=sekaiju
pkgver=8.2
pkgrel=1
pkgdesc="Sequencer for editing MIDI files"
arch=('i686' 'x86_64')
url="http://openmidiproject.sourceforge.jp/Sekaiju_en.html"
license=('LGPL')
depends=('wine')
makedepends=('gendesk' 'imagemagick')
optdepends=('bash-completion: tab completion support')
source=("${pkgname}-${pkgver}.zip::https://openmidiproject.opal.ne.jp/dlog.php?filename=Sekaiju8.2.zip"
        'Sekaiju.ini')
sha256sums=('9ad90021ed48fc0438f64b8bfdc9c052d2413db233632e261b864a25bd0178c3'
            '2b7abef2184c3ad6a4d71b094e68168af90c9fdf3b37d4747706654290b366bb'
)

prepare(){
  magick "$srcdir/Sekaiju8.2/res/SekaijuType.ico" "$srcdir/icon.png"
  gendesk -n --pkgname "$pkgname" --pkgdesc "$pkgdesc" \
    --name='Sekaiju' \
    --mimetype='audio/midi' \
    --categories 'Audio;Sequencer;Midi;Music;'
}

package(){
  mkdir -p $pkgdir/usr/share
  mkdir -p $pkgdir/usr/bin
  mkdir -p $pkgdir/usr/share/bash-completion/completions
  cp -R $srcdir/Sekaiju8.2 $pkgdir/usr/share/$pkgname
  echo -e '#!/bin/bash\n[[ "$1" == "" ]] && wine /usr/share/sekaiju/Sekaiju.exe\n[[ "$1" != "" ]] && wine /usr/share/sekaiju/Sekaiju.exe "$(winepath -w "$1")"'>$pkgdir/usr/bin/sekaiju
  chmod +x $pkgdir/usr/bin/$pkgname
  install -Dm644 "$srcdir/icon.png" "$pkgdir/usr/share/pixmaps/$pkgname.png"
  install -Dm644 "$pkgname.desktop" "$pkgdir/usr/share/applications/$pkgname.desktop"

  # Add a custom config to set the WINE MIDI Mapper as default output
  # and prevent autosaving, which appears to be totally broken on Linux
  install -Dm644 "Sekaiju.ini" "$pkgdir/usr/share/$pkgname/Sekaiju.ini"

  echo "_$pkgname() { local cur prev words cword split; _init_completion -s || return; _filedir '@(skj|chy|mid|csv|vsq)'; } && complete -F _$pkgname $pkgname" >$pkgdir/usr/share/bash-completion/completions/sekaiju
}

