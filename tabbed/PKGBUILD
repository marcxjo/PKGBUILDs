# Maintainer: Mark Givens <marcxjo@cerebralbleach.com>
# Contributor: Axel McLaren <scm(at)axml(dot)uk>
# Contributor: Yusuf Aktepe <yusuf@yusufaktepe.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: totoloco <totoloco at gmx>

pkgname=tabbed
pkgver=0.8
pkgrel=3
pkgdesc='simple generic tabbed frontend to xembed-aware applications'
arch=('x86_64')
url='https://tools.suckless.org/tabbed'
license=('custom:MIT/X')
depends=('libx11' 'libxft')
install='tabbed.install'
source=("https://dl.suckless.org/tools/${pkgname}-${pkgver}.tar.gz"
        'alpha-20241221.diff'
        'config.h')
sha256sums=('95bdffccb071083068d2b555c2524e9c7c57c9b64494d46c697e678d49a0a3d7'
            '4f726d3c26501dc5a2006d70f14689d287cba27cb64f7ec20b64fba51acc0b95'
            '84b8906a01b866880e436f4dc1d764b9df92fdae5793055c69d8a77fda747208')

prepare() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  patch -p1 <../alpha-20241221.diff
}

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  cp "${srcdir}/config.h" config.h

  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
