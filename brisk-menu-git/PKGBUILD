# Maintainer:
# Contributor: Frederic Bezies <fredbezies at gmail dot com>

_pkgname=brisk-menu
pkgname=${_pkgname}-git
pkgver=0.6.2.r5.g8a9a93f
pkgrel=1
pkgdesc='Modern, efficient menu for the MATE Desktop Environment - Ionic fork'
arch=('i686' 'x86_64')
url='https://github.com/Ionic/brisk-menu'
license=('GPL2')
groups=('mate-extra')
depends=('mate-panel' 'libnotify')
makedepends=('gnome-common' 'gettext' 'itstool' 'vala>=0.36' 'meson' 'ninja' 'glib2-devel' 'git')
optdepends=(
  'mozo: for menu edition'
  'menulibre: for menu edition'
)
options=('!libtool' '!emptydirs')
provides=("$_pkgname=${pkgver%%.r*}")
conflicts=("$_pkgname")
source=("$_pkgname"::"git+$url.git#branch=feature/customizable-icon")
sha256sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags --abbrev=7 --exclude='*[a-zA-Z][a-zA-Z]*' |
    sed -E 's/^[^0-9]*//;s/([^-]*-g)/r\1/;s/-/./g'
}

build() {
  CFLAGS="${CFLAGS/_FORTIFY_SOURCE=?/_FORTIFY_SOURCE=2}"

  arch-meson "$_pkgname" build
  meson compile -C build
}

package() {
  meson install -C build --destdir "$pkgdir"
}
