# Contributor: Emil Renner Berthing <esmil@mailme.dk>
# Contributor: Elis Axelsson <etu@failar.nu>
# Maintainer: MarÄ‰jo Givens <marcxjo@cerebralbleach.com>

_pkgname=stupidterm
pkgname=${_pkgname}-git
pkgver=v1.r24.gf824e41
pkgrel=1
pkgdesc="A Stupid Terminal based on VTE"
arch=("i686" "x86_64" "armv5tel" "armv7l")
url="https://github.com/esmil/stupidterm"
license=("LGPL")
depends=("vte3")
conflicts=("stupidterm" "st")
source=(
    "${_pkgname}::git+https://github.com/esmil/stupidterm.git"
    'st.desktop'
    'allow_bold_is_bright.patch'
    'rename_binary.patch'
)
md5sums=(
    'SKIP'
    '71c75bd8466eeb85161e27009231e038'
    '5d95363b1bc6588f3b4576e49007f24c'
    '73490e745a1557d61c7bea2b8b19dcb2'
)
provides=(
    'stupidterm'
)

pkgver() {
    cd "${srcdir}/${_pkgname}"
    git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${_pkgname}"
    patch -p1 <../allow_bold_is_bright.patch
    patch -p1 <../rename_binary.patch
}

build() {
    cd "${srcdir}/${_pkgname}"

    make clean
    make release
}

package() {
    cd "${srcdir}/${_pkgname}"

    make DESTDIR="${pkgdir}" prefix=/usr install

    # Create paths for .desktop and config
    mkdir -p $pkgdir/usr/share/applications/ $pkgdir/usr/share/stupidterm/

    # Copy .desktop file to package
    cat $srcdir/st.desktop > $pkgdir/usr/share/applications/st.desktop

    # Copy default config file
    cat $srcdir/stupidterm/stupidterm.ini \
        > $pkgdir/usr/share/stupidterm/stupidterm.ini
}
